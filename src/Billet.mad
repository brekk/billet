import type { Comparison } from "Compare"
import type { Maybe } from "Maybe"

import { EQ, GT, LT } from "Compare"
import { equals, when } from "Function"
import IO from "IO"
import List from "List"
import { Just, Nothing, fromMaybe, isJust } from "Maybe"
import Print from "PrettyPrint"
import String from "String"



export alias Scope = List String
export type Tag = Tag(Boolean, Scope, String)

instance Eq Tag {
  __EQ__ = (a, b) => serializeTag(a) == serializeTag(b)
}
instance Comparable Tag {
  compare = compareTag
}

scopedTag :: Scope -> String -> Tag
export scopedTag = Tag(true)

export scopedUnTag = Tag(false)

export tag = scopedTag([])

isScoped :: Tag -> Boolean
export isScoped = where {
  Tag(_, scope, _) =>
    List.length(scope) > 0
}

getScope :: Tag -> Scope
export getScope = where {
  Tag(_, scope, _) =>
    scope
}

getId :: Tag -> String
export getId = where {
  Tag(_, _, name) =>
    name
}

scopeLength :: Tag -> Integer
export scopeLength = pipe(
  getScope,
  List.length,
)

compareTag :: Tag -> Tag -> Comparison
export compareTag = (a, b) => {
  aName = getId(a)
  bName = getId(b)

  return if (bName == aName) do {
    sA = scopeLength(a)
    sB = scopeLength(b)
    return if (sA == sB) {
      EQ
    } else if (sA > sB) {
      GT
    } else {
      LT
    }
  } else {
    EQ
  }
}

// can we compare Maybes with syntax?
scopeEqual :: Scope -> Scope -> Boolean
export scopeEqual = (a, b) => where(#[a, b]) {
  #[scope1, scope2] =>
    scope1 == scope2
}

hasScope :: Scope -> Tag -> Boolean
export hasScope = (testScope, t) => pipe(
  getScope,
  scopeEqual(testScope),
)(t)

scopeWithinScope :: Scope -> Scope -> Boolean
export scopeWithinScope = (parent, child) => {
  lenPar = List.length(parent)
  lenChild = List.length(child)
  return if (lenChild == 0) {
    true
  } else if (lenPar <= lenChild) {
    pipe(
      List.zip(parent),
      List.all(where { #[a, b] => a == b }),
    )(child)
  } else {
    false
  }
}

tagInScope :: Scope -> Tag -> Boolean
export tagInScope = (testScope, t) => pipe(
  getScope,
  scopeWithinScope(testScope),
)(t)

checkScopes :: List Scope -> Tag -> Boolean
export checkScopes = (scopes, t) => List.any(tagInScope($, t), scopes)

serializeTag :: Tag -> String
export serializeTag = (t) => {
  scope = pipe(
    getScope,
    // List.append("*"),
    String.join(":"),
  )(t)
  name = getId(t)
  return name ++ (equals("", scope) ? "" : ":" ++ scope)
}

checkTagScopes :: List Tag -> Tag -> Boolean
export checkTagScopes = (tags, t) => pipe(
  List.filter(
    pipe(
      getId,
      pipe(
        getId,
        equals,
      )(t),
    ),
  ),
  map(getScope),
  checkScopes($, t),
)(tags)


firstAsString :: List String -> String
firstAsString = pipe(
  List.first,
  fromMaybe(""),
)

parseTag :: String -> Tag
parseTag = (part) => {
  negated = !String.startsWith("-", part)
  return pipe(
    String.trim,
    String.split(":"),
    (list) => {
      segments = List.length(list) == 1 ? [] : List.tail(list)
      return pipe(
        firstAsString,
        Tag(negated, segments),
      )(list)
    },
  )(part)
}

isEmptyTag :: Tag -> Boolean
isEmptyTag = pipe(
  getId,
  equals(""),
)

parseTags :: String -> List Tag
export parseTags = pipe(
  String.split(","),
  map(parseTag),
  List.reject(isEmptyTag),
)


// log :: Show a => List (Maybe String) -> Tag -> a -> {}
export makeScopedLogger = (tags, t, thing) => {
  return if (checkTagScopes(tags, t)) do {
    IO.pTrace(serializeTag(t), thing)
    return thing
  } else do {
    IO.putLine(`<<<< SKIPPED: ${serializeTag(t)} >>>>`)
    return thing
  }
}

export makeLogger = (tags, t, thing) => pipe(
  parseTag,
  makeScopedLogger(tags, $, thing),
)(t)
