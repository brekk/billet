import type { Maybe } from "Maybe"

import type { Tag } from "@/Tag"

import { Just, Nothing } from "Maybe"

import { ansiFgBg, seededAnsi } from "@/Color"
import { parse, serialize, within } from "@/Tag"



export alias Trace b c = String -> b -> c
export alias Invocable a b c = Trace b c -> List Tag -> Tag -> a -> b

composed :: Invocable String String String
export composed = (run, tags, t, thing) => (within(tags, t) ? run(serialize(t), thing) : thing)

captured :: Trace String String -> List Tag -> Tag -> String -> Maybe String
export captured = (run, tags, t, thing) => if (!within(tags, t)) {
  Nothing
} else {
  pipe(
    run(serialize(t)),
    Just,
  )(thing)
}

parsed :: Trace String String -> List Tag -> String -> String -> String
export parsed = (run, tags, t, thing) => pipe(
  parse,
  composed(run, tags, $, thing),
)(t)

styled :: String -> Trace String String -> List Tag -> String -> String -> String
export styled = (seedPrefix, run, raw) => parsed(
  (t, x) => pipe(
    seededAnsi(seedPrefix),
    ansiFgBg(t),
    run($, x),
  )(t),
)(raw)
