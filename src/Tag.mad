import type { Comparison } from "Compare"

import type { Scope } from "@/Scope"

import { EQ, GT, LT } from "Compare"
import { always, equals, when } from "Function"
import List from "List"
import { fromMaybe } from "Maybe"
import String from "String"

import { scopeEquals, withinScope } from "@/Scope"



export type Tag = Tag(Boolean, Scope, String)

instance Eq Tag {
  __EQ__ = (a, b) => serialize(a) == serialize(b)
}
instance Comparable Tag {
  compare = compareTag
}

scopedTag :: Scope -> String -> Tag
export scopedTag = Tag(true)

export scopedUnTag = Tag(false)

export tag = scopedTag([])
export untag = scopedUnTag([])

isNegated :: Tag -> Boolean
export isNegated = where {
  Tag(x, _, _) =>
    !x
}

isScoped :: Tag -> Boolean
export isScoped = where {
  Tag(_, scope, _) =>
    List.length(scope) > 0
}

getScope :: Tag -> Scope
export getScope = where {
  Tag(_, scope, _) =>
    scope
}

id :: Tag -> String
export id = where {
  Tag(_, _, name) =>
    name
}

scopeLength :: Tag -> Integer
export scopeLength = pipe(
  getScope,
  List.length,
)

compareTag :: Tag -> Tag -> Comparison
export compareTag = (a, b) => {
  aName = id(a)
  bName = id(b)

  return if (bName == aName) do {
    sA = scopeLength(a)
    sB = scopeLength(b)
    return if (sA == sB) {
      EQ
    } else if (sA > sB) {
      GT
    } else {
      LT
    }
  } else {
    EQ
  }
}

hasScope :: Scope -> Tag -> Boolean
export hasScope = (testScope, t) => pipe(
  getScope,
  scopeEquals(testScope),
)(t)

_firstAsString :: List String -> String
_firstAsString = pipe(
  List.first,
  fromMaybe(""),
)

_isStarTag :: List String -> Boolean
_isStarTag = List.endsWith(["*"])

inScope :: Scope -> Tag -> Boolean
export inScope = (testScope, t) => pipe(
  getScope,
  when(_isStarTag, always([])),
  withinScope(testScope),
)(t)

checkScopes :: Tag -> List Scope -> Boolean
export checkScopes = (t, scopes) => List.any(inScope($, t), scopes)

serialize :: Tag -> String
export serialize = (t) => {
  scope = pipe(
    getScope,
    String.join(":"),
  )(t)
  name = id(t)
  return name ++ (equals("", scope) ? "" : ":" ++ scope)
}

// TODO: support negated tags next
within :: List Tag -> Tag -> Boolean
export within = (tags, t) => {
  tId = id(t)
  return pipe(
    List.filter(
      pipe(
        id,
        (uId) => tId == "*" || uId == tId,
      ),
    ),
    map(getScope),
    checkScopes(t),
  )(tags)
}

// eventually we could actually parse stuff
parse :: String -> Tag
export parse = (part) => {
  negated = !String.startsWith("-", part)
  return pipe(
    String.trim,
    String.split(":"),
    (list) => {
      segments = List.length(list) == 1 ? [] : List.tail(list)
      return pipe(
        _firstAsString,
        Tag(negated, segments),
      )(list)
    },
  )(part)
}

_isEmptyTag :: Tag -> Boolean
_isEmptyTag = pipe(
  id,
  equals(""),
)

parseMany :: String -> List Tag
export parseMany = pipe(
  String.split(","),
  map(parse),
  List.reject(_isEmptyTag),
)
